#!/usr/bin/env tclsh
###############################################################################
# BRLTTY - A background process providing access to the console screen (when in
#          text mode) for a blind person using a refreshable braille display.
#
# Copyright (C) 1995-2021 by The BRLTTY Developers.
#
# BRLTTY comes with ABSOLUTELY NO WARRANTY.
#
# This is free software, placed under the terms of the
# GNU Lesser General Public License, as published by the Free Software
# Foundation; either version 2.1 of the License, or (at your option) any
# later version. Please see the file LICENSE-LGPL for details.
#
# Web Page: http://brltty.app/
#
# This software is maintained by Dave Mielke <dave@mielke.cc>.
###############################################################################

source [file join [file dirname [info script]] .. prologue.tcl]

set optionDefinitions {
}

processProgramArguments optionValues $optionDefinitions

proc makeLists {} {
   set file "brltty.conf.in"
   set pattern {^#(\w+-(?:driver|table))\s+([-\w]+)\s*#\s*(.*?)\s*$}
   set lists [dict create]

   forEachLine line $file {
      if {[regexp $pattern $line x type name description]} {
         dict lappend lists $type [list $name $description]
      }
   }

   return $lists
}

proc quoteColumn {text} {
   return "\"[regsub -all {(")} $text {\1\1}]\""
}

proc makeData {rows} {
   set data ""

   foreach row $rows {
      append data "[join [lmap column $row {quoteColumn $column}] ,]\n"
   }

   return $data
}

proc updateFile {name rows} {
   set data [makeData $rows]

   if {![file exists $name]} {
      logMessage warning "creating file: $name"
      writeFile $name $data
   } elseif {![string equal $data [readFile $name]]} {
      logMessage notice "updating file: $name"
      replaceFile $name $data
   } else {
      return 0
   }

   return 1
}

proc updateFiles {lists} {
   set updated 0

   foreach type [dict keys $lists] {
      if {[updateFile "$type.csv" [dict get $lists $type]]} {
         set updated 1
      }
   }

   return $updated
}

setSourceRoot
cd [file join $env(BRLTTY_SOURCE_ROOT) Documents]

if {![updateFiles [makeLists]]} {
   logMessage task "no files updated"
}

exit 0
