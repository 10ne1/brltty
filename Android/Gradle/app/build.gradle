description = """
"${project.name}" implements the accessibility service for "${rootProject.name}" on Android.
"""

apply plugin: "com.android.application"
apply from: "brltty.gradle"

android {
  compileSdkVersion "android-29"
  buildToolsVersion "29.0.3"

  defaultConfig {
    targetSdkVersion 29
    minSdkVersion 16

    versionName appVersionName
    versionCode appVersionCode

    ndk {
      abiFilters 'armeabi-v7a','arm64-v8a','x86','x86_64'
    }

    configuredProperties.propertyNames().each { name ->
      resValue "string", "appConfig_${name}", configuredProperties[name]
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    warning "MissingTranslation"
    error "Untranslatable"
  }

  signingConfigs {
    release {
      storeFile     credentialProperties.storeFile
      storePassword credentialProperties.storePassword
      keyAlias      credentialProperties.keyAlias
      keyPassword   credentialProperties.keyPassword
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFile getDefaultProguardFile("proguard-android.txt")
    }
  }
}

dependencies {
  implementation project(":core")
  implementation "androidx.core:core:1.1.0"
}
