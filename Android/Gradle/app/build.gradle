plugins {
  id "com.android.application"
  id "com.github.triplet.play" version "2.5.0"
}

description = """
"${project.name}" implements the accessibility service for "${rootProject.name}" on Android.
"""

apply from: "brltty.gradle"

dependencies {
  implementation project(":core")
  implementation "androidx.core:core:1.1.0"
}

android {
  compileSdkVersion "android-29"
  buildToolsVersion "29.0.3"

  defaultConfig {
    targetSdkVersion 29
    minSdkVersion 16

    versionName appVersionName
    versionCode appVersionCode

    brltty.config.propertyNames().each { name ->
      resValue "string", "appConfig_${name}", brltty.config[name]
    }

    ndk {
      splits {
        abi {
          enable true
          universalApk true
          reset()

          brltty.native.abiList.each { abi ->
            include abi
          }
        }
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    warning "MissingTranslation"
    error "Untranslatable"
  }

  signingConfigs {
    release {
      def signingProperties = brltty.loadProperties("credentials/sign-gradle.properties")
      storeFile     signingProperties.storeFile
      storePassword signingProperties.storePassword
      keyAlias      signingProperties.keyAlias
      keyPassword   signingProperties.keyPassword
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      minifyEnabled false
      proguardFile getDefaultProguardFile("proguard-android.txt")
    }
  }

  play {
    track = "production" // internal, alpha, beta, production
    serviceAccountCredentials = rootProject.file("credentials/publish-play.json")
  }
}
