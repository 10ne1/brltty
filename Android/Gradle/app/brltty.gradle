def assetsDirectory = projectDir.absolutePath + "/src/main/assets"
def driversDirectory = assetsDirectory + File.separator + "drivers"
def messagesDirectory = assetsDirectory + File.separator + "locale"
def tablesDirectory = assetsDirectory + File.separator + "tables"

ext {
  appVersionIdentifier = [
    nativeProperties.rootDirectory + File.separator + "getrevid",
    nativeProperties.rootDirectory
  ].execute().text.trim()
}

ext {
  appVersionName = appVersionIdentifier.replaceAll(/^.*?-(\d)/, /$1/).replaceAll(/-g.*$/, "")
}

ext {
  appVersionCode = [
    nativeProperties.rootDirectory + File.separator + "getrevid",
    "-r", appVersionIdentifier, "-i",
    nativeProperties.rootDirectory
  ].execute().text.trim() as int
}

task addTables (type: Copy) {
  from nativeProperties.tablesDirectory
  into tablesDirectory
  include 'Text/**/*.tt?'
  include 'Attributes/**/*.at?'
  include 'Contraction/**/*.ct?'
  include 'Keyboard/**/*.kt?'
  include 'Input/**/*.kt?'
}

task removeTables (type: Delete) {
  delete tablesDirectory
}

task addDrivers (type: Copy) {
  from nativeProperties.driversDirectory
  into driversDirectory
  include 'libbrlttyb??.so'
  include 'libbrlttys??.so'
  include 'libbrlttyx??.so'
}

task removeDrivers (type: Delete) {
  delete driversDirectory
}

tasks.register("addMessages") {
  doLast {
    def commandProcesses = []

    try {
      def sourceDirectory = new File(nativeProperties.messagesDirectory)
      def sourceFiles = layout.files {sourceDirectory.listFiles()}

      sourceFiles.each { sourceFile ->
        def sourceName = sourceFile.name

        if (sourceName.endsWith(".po")) {
          def languageCode = sourceName.replaceAll(/\..+?$/, '')
          def targetDirectory = new File(messagesDirectory + File.separator + languageCode + File.separator + "LC_MESSAGES")

          if (targetDirectory.isDirectory() || targetDirectory.mkdirs()) {
            def targetFile = new File(targetDirectory, "brltty.mo")

            def commandArguments = [
              "msgfmt", "--no-hash",
              "--output-file=" + targetFile.absolutePath,
              "--", sourceFile.absolutePath
            ]

            def commandProcess = commandArguments.execute()
            commandProcess.waitForProcessOutput(new StringBuffer(), System.err)
            commandProcesses += commandProcess
          } else {
            throw new IOException("messages directory not created: " + targetDirectory.absolutePath)
          }
        }
      }
    } finally {
      def ok = true

      commandProcesses.each { process ->
        int exitValue = process.waitFor()
        if (exitValue) ok = false
      }

      if (!ok) throw new RuntimeException("installing messages failed")
    }
  }
}

task removeMessages (type: Delete) {
  delete messagesDirectory
}

nativeComponents.assembleTask.dependsOn addDrivers, addMessages, addTables
nativeComponents.cleanTask.dependsOn removeDrivers, removeMessages, removeTables

void addAssembleTask (Task task) {
  ["assembleDebug", "assembleRelease"].each { taskName ->
    tasks[taskName].dependsOn task
  }
}

afterEvaluate {
  [nativeComponents.assembleTask].each { task ->
    addAssembleTask task
  }
}
