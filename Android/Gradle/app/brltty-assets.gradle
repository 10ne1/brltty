def assetsDirectory = brltty.getAssetsPath(project)
def documentsDirectory = assetsDirectory + File.separator + "documents"
def messagesDirectory = assetsDirectory + File.separator + "locale"
def tablesDirectory = assetsDirectory + File.separator + "tables"

task brlttyMakeDocuments (type: Exec) {
  group "build"
  description "Make the on-device documents."

  workingDir brltty.native.documentsDirectory
  ignoreExitValue false

  commandLine "make"
  args brltty.native.documentsList
}

task brlttyAddDocuments (type: Copy) {
  group "build"
  description "Add the on-device documents to the build (as assets)."
  dependsOn brlttyMakeDocuments

  from brltty.native.documentsDirectory
  into documentsDirectory
  include brltty.native.documentsList
}

task brlttyRemoveDocuments (type: Delete) {
  group "build"
  description "Remove the on-device documents from the build."

  delete documentsDirectory
}

tasks.register("brlttyAddMessages") {
  group "build"
  description "Add the message catalogs (.mo files) to the build (as assets)."

  doLast {
    def commandProcesses = []

    try {
      def sourceDirectory = new File(brltty.native.messagesDirectory)
      def sourceFiles = layout.files {sourceDirectory.listFiles()}

      sourceFiles.each { sourceFile ->
        def sourceName = sourceFile.name

        if (sourceName.endsWith(".po")) {
          def languageCode = sourceName.replaceAll(/\..+?$/, '')
          def targetDirectory = new File(messagesDirectory + File.separator + languageCode + File.separator + "LC_MESSAGES")

          if (targetDirectory.isDirectory() || targetDirectory.mkdirs()) {
            def targetFile = new File(targetDirectory, "brltty.mo")

            def commandArguments = [
              "msgfmt", "--no-hash",
              "--output-file=" + targetFile.absolutePath,
              "--", sourceFile.absolutePath
            ]

            def commandProcess = commandArguments.execute()
            commandProcess.waitForProcessOutput(new StringBuffer(), System.err)
            commandProcesses += commandProcess
          } else {
            throw new IOException("messages directory not created: " + targetDirectory.absolutePath)
          }
        }
      }
    } finally {
      def ok = true

      commandProcesses.each { process ->
        int exitValue = process.waitFor()
        if (exitValue) ok = false
      }

      if (!ok) throw new RuntimeException("installing messages failed")
    }
  }
}

task brlttyRemoveMessages (type: Delete) {
  group "build"
  description "Remove the message catalogs (.mo files) from the build."

  delete messagesDirectory
}

task brlttyAddTables (type: Copy) {
  group "build"
  description "Add the tables (text, attributes, contraction, keyboard, input) to the build (as assets)."

  from brltty.native.tablesDirectory
  into tablesDirectory
  include 'Text/**/*.tt?'
  include 'Attributes/**/*.at?'
  include 'Contraction/**/*.ct?'
  include 'Keyboard/**/*.kt?'
  include 'Input/**/*.kt?'
}

task brlttyRemoveTables (type: Delete) {
  group "build"
  description "Remove the tables (text, attributes, contraction, keyboard, input) from the build."

  delete tablesDirectory
}

brltty.addAssembleTask project, brlttyAddDocuments
brltty.addAssembleTask project, brlttyAddMessages
brltty.addAssembleTask project, brlttyAddTables

clean.dependsOn brlttyRemoveDocuments
clean.dependsOn brlttyRemoveMessages
clean.dependsOn brlttyRemoveTables
