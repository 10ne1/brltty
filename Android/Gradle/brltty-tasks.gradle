String getJarPath (Map properties) {
  return brltty.getJarPath(properties.project)
}

String getJNIPath (Map properties) {
  return brltty.getJNIPath(properties.project)
}

String getABIPath (Map properties) {
  return brltty.getABIPath(properties.project, properties.ABI)
}

Map getABI (String abi) {
  if (!brltty.containsKey("ABI")) {
    brltty.ABI = [:]
  }

  def ABI = brltty.ABI
  if (!ABI.containsKey(abi)) ABI[abi] = [:]
  return ABI[abi]
}

Map getABI (Map properties) {
  return getABI(properties.ABI)
}

Task newTask (Project project, String name, Class<DefaultTask> type=DefaultTask, Closure closure) {
  return project.tasks.register(name, type, closure).get()
}

Task newTask (Map properties, String name, Class<DefaultTask> type=DefaultTask, Closure closure) {
  name += properties.taskNameSuffix
  return newTask(properties.project, name, type, closure)
}

Task newAssembleTask () {
  return newTask(rootProject, "brlttyAssembleAllComponents") {
    description "Assemble all of the native jars, libraries, assets, etc"
  }
}

void addAssembleDependency (Task dependentTask) {
  brltty.assembleTask.dependsOn dependentTask
}

Task newCleanTask () {
  return newTask(rootProject, "brlttyCleanAllComponents") {
    description "Clean all of the native jars, libraries, assets, etc"
  }
}

void addCleanDependency (Task dependentTask) {
  brltty.cleanTask.dependsOn dependentTask
}

Task newConfigureTask (Map properties) {
  return newTask(properties, "brlttyConfigureABI", Exec) {
    description "Configure the ${properties.component} for the ${properties.ABI} ABI"

    workingDir properties.topDirectory
    ignoreExitValue false
    commandLine "${brltty.native.rootDirectory}/cfg-android-abi", "-q", properties.ABI
  }
}

Task getConfigureTask (Map properties) {
  def abi = getABI(properties)

  if (!abi.containsKey("configureTask")) {
    abi.configureTask = newConfigureTask(properties)
  }

  return abi.configureTask
}

Task newMakeComponentsTask (Map properties) {
  return newTask(properties, "brlttyMakeABI", Exec) {
    description "Make the ${properties.ABI} components of the ${properties.component}"
    dependsOn properties.configureTask

    workingDir properties.componentsDirectory
    ignoreExitValue false

    commandLine "make"
    args properties.make
  }
}

Task newAddJarsTask (Map properties) {
  return newTask(properties, "brlttyAddJars", Copy) {
    description "Add the ${properties.component}'s ${properties.ABI} jars to the build"
    dependsOn properties.makeComponentsTask

    into getJarPath(properties)
    from properties.componentsDirectory

    properties.jars.each { jar ->
      include "${jar}.jar"
    }
  }
}

Task newAddLibrariesTask (Map properties) {
  return newTask(properties, "brlttyAddLibraries", Copy) {
    description "Add the ${properties.component}'s ${properties.ABI} libraries to the build"
    dependsOn properties.makeComponentsTask

    def componentsDirectory =  properties.componentsDirectory
    def coreDirectory = properties.coreComponentsDirectory

    into getABIPath(properties)
    from componentsDirectory

    if (!coreDirectory.equals(componentsDirectory)) {
      from coreDirectory
    }

    properties.libs.each { library ->
      include "lib${library}.so"
    }
  }
}

Task newMakeCleanTask (Map properties) {
  return newTask(properties, "brlttyCleanABI", Exec) {
    description "Remove (make clean) the ${properties.ABI} components of the ${properties.component}"
    dependsOn properties.configureTask

    workingDir properties.componentsDirectory
    ignoreExitValue true
    commandLine "make", "clean"
  }
}

Task newRemoveTask (Map properties) {
  return newTask(properties, "brlttyRemoveAllComponents", Delete) {
    description "Remove the ${properties.component}'s jars and libraries from the build"
    delete getJarPath(properties)
    delete getJNIPath(properties)
  }
}

void addTasks (Map properties, String abi) {
  properties.ABI = abi
  def isPrimaryABI = abi.equals(brltty.native.abiList[0])

  properties.topDirectory = brltty.native.abiDirectory + File.separator + properties.ABI
  properties.configureTask = getConfigureTask(properties)

  properties.componentsDirectory = properties.topDirectory + File.separator + properties.subdirectory
  properties.coreComponentsDirectory = properties.topDirectory + File.separator + brltty.native.coreSubdirectory
  properties.makeComponentsTask = newMakeComponentsTask(properties)

  if (isPrimaryABI) {
    def jars = properties.jars

    if ((jars != null) && !jars.isEmpty()) {
      def jarsTask = newAddJarsTask(properties)
      addAssembleDependency jarsTask
    }
  }

  def libraries = properties.libs
  if ((libraries != null) && !libraries.isEmpty()) {
    def addLibrariesTask = newAddLibrariesTask(properties)
    addAssembleDependency addLibrariesTask
  }

  def makeCleanTask = newMakeCleanTask(properties)
  addCleanDependency makeCleanTask
}

void addTasks (Map properties) {
  def taskNameSuffix = ""
  properties.taskNameSuffix = taskNameSuffix

  if (properties.make == null) properties.make = "all"
  if (properties.make instanceof String) properties.make = [properties.make]

  def removeTask = newRemoveTask(properties)
  addCleanDependency removeTask

  brltty.native.abiList.each { abi ->
    properties.taskNameSuffix = "${taskNameSuffix}_${abi}"
    addTasks(properties, abi)
  }
}

brltty.assembleTask = newAssembleTask()
brltty.cleanTask = newCleanTask()

brltty.addTasks = { Map properties ->
  addTasks(properties)
}

clean.dependsOn brltty.cleanTask
