def propertyFiles = [
  configuredProperties: "config.properties",
  nativeProperties: "native.properties",
  signingProperties: "signing/gradle.properties"
]

void normalizeFileProperty (Properties properties, String name) {
  properties[name] = rootProject.file(properties[name])
}

void normalizeDirectoryProperty (Properties properties, String name) {
  normalizeFileProperty(properties, name)
  properties[name] = properties[name].absolutePath
}

void normalizeSubdirectoryProperty (Properties properties, String subdirectoryName) {
  def directoryName = subdirectoryName.replaceAll(/Subdirectory$/, "Directory")
  properties[directoryName] = properties.rootDirectory + File.separator + properties[subdirectoryName]
}

Properties loadPropertiesFromFile (String file) {
  def properties = new Properties()
  properties.load(new FileInputStream(rootProject.file(file)))

  def directoryProperties = []
  def subdirectoryProperties = []
  def fileProperties = []

  properties.propertyNames().each { name ->
    if (name.endsWith("Directory")) {
      directoryProperties += name
    } else if (name.endsWith("Subdirectory")) {
      subdirectoryProperties += name
    } else if (name.endsWith("File")) {
      fileProperties += name
    }
  }

  directoryProperties.each { name ->
    normalizeDirectoryProperty(properties, name)
  }

  subdirectoryProperties.each { name ->
    normalizeSubdirectoryProperty(properties, name)
  }

  fileProperties.each { name ->
    normalizeFileProperty(properties, name)
  }

  return properties
}

propertyFiles.keySet().each { name ->
  def path = propertyFiles[name]
  ext[name] = loadPropertiesFromFile(path)
}
