def assetsDirectory = "src/main/assets"

task installDrivers (type: Copy) {
  from nativeProperties.driversDirectory
  into assetsDirectory + "/drivers"
  include 'libbrlttyb??.so'
  include 'libbrlttys??.so'
  include 'libbrlttyx??.so'
}

task installTables (type: Copy) {
  from nativeProperties.tablesDirectory
  into assetsDirectory + "/tables"
  include 'Text/**/*.tt?'
  include 'Attributes/**/*.at?'
  include 'Contraction/**/*.ct?'
  include 'Keyboard/**/*.kt?'
  include 'Input/**/*.kt?'
}

void addAssembleTask (Task task) {
  ["assembleDebug", "assembleRelease"].each { taskName ->
    tasks[taskName].dependsOn task
  }
}

afterEvaluate {
  addAssembleTask installTables
  addAssembleTask installDrivers
  installDrivers.dependsOn ":core:makeCoreComponents"
}
