task makeCoreComponents (type: Exec) {
  workingDir nativeProperties.coreDirectory
  commandLine 'make', nativeProperties.coreLibrary, nativeProperties.coreWrapper, nativeProperties.coreBindings
  ignoreExitValue false
}

task cleanCoreComponents (type: Exec) {
  workingDir nativeProperties.coreDirectory
  commandLine 'make', 'clean'
  ignoreExitValue true
}

task installCoreComponents (type: Copy) {
  from nativeProperties.coreDirectory
  into "src/main/jniLibs/armeabi"
  include "*.so"
}

task makeBindingsComponents (type: Exec) {
  workingDir nativeProperties.javaDirectory
  commandLine 'make', 'all'
  ignoreExitValue false
}

task cleanBindingsComponents (type: Exec) {
  workingDir nativeProperties.javaDirectory
  commandLine 'make', 'clean'
  ignoreExitValue true
}

task installBindingsComponents (type: Copy) {
  into "src/main"

  from(nativeProperties.javaDirectory) {
    into "libs"
    include "*.jar"
  }

  from(nativeProperties.javaDirectory) {
    into "jniLibs/armeabi"
    include "*.so"
  }
}

installCoreComponents.dependsOn makeCoreComponents
installBindingsComponents.dependsOn makeBindingsComponents

android {
  tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(installCoreComponents)
  }

  tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(installBindingsComponents)
  }

  clean.dependsOn(cleanCoreComponents)
  clean.dependsOn(cleanBindingsComponents)
}

